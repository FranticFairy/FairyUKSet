//Sprite References & Switches
spriteset(spriteset_MGR_HOPPER_purchase, "gfx/CW/MGR_HOPPER.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_MGR_HOPPER, "gfx/CW/MGR_HOPPER.png") { template_fai_25(0, 16)}
spriteset(spriteset_MGR_HOPPER_R, "gfx/CW/MGR_HOPPER.png") { template_fai_25(0, 41)}

spriteset(spriteset_MGR_HOPPER_COAL_H, "gfx/CW/MGR_HOPPER_COAL.png") { template_fai_25(0, 41)}
spriteset(spriteset_MGR_HOPPER_COAL_F, "gfx/CW/MGR_HOPPER_COAL.png") { template_fai_25(0, 66)}

switch(FEAT_TRAINS, SELF, sw_MGR_HOPPER_loadcheck_COAL, PERCENT_FULL){
	1..50	: spriteset_MGR_HOPPER_COAL_H ;
	51..100	: spriteset_MGR_HOPPER_COAL_F ;
}

random_switch (FEAT_TRAINS, SELF, sw_MGR_HOPPER_randomtexture) {
	1: spriteset_MGR_HOPPER;
	1: spriteset_MGR_HOPPER_R;
}

switch(FEAT_TRAINS, SELF, sw_MGR_HOPPER_spritestack, SPR_STACK){
	0 : sw_MGR_HOPPER_randomtexture ;
	1 : sw_MGR_HOPPER_loadcheck_COAL ;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_CW_MGR_HOPPER) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		
		track_type:						RAIL;
		
		name:							string(STR_NAME_MGR_HOPPER); 
		introduction_date:				date(1970,1,1);
		model_life:						55;
		length:							6;
		
		weight: 						16 ton;
		speed:							65 mph;
		cost_factor: 					0x2C nfo;
		running_cost_factor: 			0x0E nfo;
		
		refittable_cargo_classes:		bitmask(CC_BULK);
		non_refittable_cargo_classes:		bitmask(CC_PASSENGERS,CC_COVERED);
		default_cargo_type:				COAL;
		cargo_capacity:					25;
		
	}
	graphics {
		default:						sw_MGR_HOPPER_spritestack;
		purchase:						spriteset_MGR_HOPPER_purchase;
	}
}
